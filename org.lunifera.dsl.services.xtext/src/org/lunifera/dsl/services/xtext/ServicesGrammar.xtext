grammar org.lunifera.dsl.services.xtext.ServicesGrammar with org.lunifera.dsl.common.xtext.CommonGrammar

import "http://www.lunifera.org/dsl/dto/v1" as dto
import "http://www.lunifera.org/dsl/services/v1" as service
import "http://www.lunifera.org/dsl/common/types/v1" as common
import "http://www.lunifera.org/dsl/entity/v1" as entity
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.eclipse.org/Xtext/Xbase/XAnnotations" as xannotation
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

LServiceModel returns service::LServiceModel:
	packages+=TypedPackage*;

Class returns service::LDTOService:
	{service::LDTOService} annotations+=AnnotationDef*
	{service::LDTOService.annotationInfo=current}
	'dtoservice' name=ValidIDWithKeywords
	'{'
	(('primary dto' primaryDTO=[dto::LDto] ';') &
	(supportedDTOs=SupportedDTOCollection)?)
	'}';

SupportedDTOCollection returns service::LSupportedDTOCollection:
	{service::LSupportedDTOCollection}
	'supportedDTOs' '{'
	(supportedDtos+=SupportedDTO)*
	'}';

SupportedDTO returns service::LSupportedDTO:
	dto=[dto::LDto]
	((filters=SupportedFilterAttributes) | ';');

SupportedFilterAttributes returns service::LSupportedFilters:
	'filters' '{'
	filterFeatures+=[dto::LDtoFeature|QualifiedName] (',' filterFeatures+=[dto::LDtoFeature|QualifiedName])* ';'
	'}';
