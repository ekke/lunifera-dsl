grammar org.lunifera.dsl.dto.xtext.EntityGrammar with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

import "http://www.lunifera.org/dsl/dto/v1" as dto
import "http://www.lunifera.org/dsl/common/types/v1"
import "http://www.lunifera.org/dsl/entity/v1" as entity
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.eclipse.org/Xtext/Xbase/XAnnotations" as xannotation
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

LDTOModel returns dto::LDtoModel:
	packages+=LTypedPackage*;

LTypedPackage:
	{LTypedPackage}
	'package' name=QualifiedName ('{'
	(imports+=LImport)*
	(types+=LType)*
	'}')?;

LImport:
	'import' importedNamespace=LQualifiedNameWithWildCard ';';

LDataType:
	'datatype' name=ID
	'jvmType' jvmTypeReference=JvmTypeReference ';';

LScalarType:
	LDataType | LEnum;

LType:
	LScalarType | Class;

Class returns LClass:
	{LClass} annotations+=LAnnotationDef*
	{dto::LDto.annotationInfo=current}
	'dto' name=ValidIDWithKeywords ('extends' superType=[dto::LDto])? ('replaces' replacedDto=[dto::LDto])?
	'{'
	features+=LDtoFeature*
	'}';

LDtoFeature returns dto::LDtoFeature:
	{dto::LDtoFeature}
	annotations+=LAnnotationDef*
	(({dto::LDtoReference.annotationInfo=current}
	('ref' (cascading?='cascade')? type=[dto::LDto] multiplicity=LMultiplicity? name=ValidIDWithKeywords
	('opposite' opposite=[dto::LDtoReference|LFQN])?)
	';')
	|
	({dto::LDtoAttribute.annotationInfo=current}
	(('transient' type=[LScalarType] name=ValidIDWithKeywords)
	|
	(('var' | id?='id' | version?="version") type=[LScalarType] multiplicity=LMultiplicity?
	name=ValidIDWithKeywords
	';')))
	|
	({dto::LDtoOperation.annotationInfo=current}
	('def' type=JvmTypeReference name=ValidIDWithKeywords
	'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
	body=XExpression)));

LModifier:
	final?='final'? & static?='static'? & visibility=LVisibility;

LEnum:
	'enum' name=ID
	('{'
	(literals+=LEnumLiteral ((',')? literals+=LEnumLiteral)*)?
	'}')?;

LEnumLiteral:
	name=ID;

LAnnotationDef:
	=> (annotation=XAnnotation);

enum LVisibility:
	PACKAGE='package' | PRIVATE='private' | PROTECTED='protected' | PUBLIC='public';

LQualifiedNameWithWildCard:
	QualifiedName ('.' '*')?;

LFQN:
	ID ('.' ID)*;

ValidIDWithKeywords:
	'cachable' | 'id' | 'transient' | 'version' |
	'collection' | 'refers' | 'settings' | 'lazy' |
	'notnull' | 'dto' |
	ID;

LMultiplicity:
	'[' (lower=LLowerBound) ('..' upper=LUpperBound)? ']';

enum LLowerBound:
	MANY='*' | ZERO='0' | OPTIONAL='?' | ATLEASTONE='+' | ONE='1';

enum LUpperBound:
	MANY='*' | ONE='1';
	
