/*******************************************************************************
 * Copyright (c) 2011, 2012 Cristiano Gavião - Lunifera.org.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Cristiano Gavião - initial API and implementation
 *******************************************************************************/
grammar org.lunifera.dsl.organization.xtext.OrganizationGrammar with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.lunifera.org/dsl/organization/v1"

OrganizationModel:
	((organization=Organization) |
	(organizationUnitTypes+=OrganizationUnitType*))
;

Organization:
	'organization' name=ID ('{'
		(('name' longName=STRING)? &
		('version' version=Fqn)? &
		('description' description=STRING)? &
		(elements+=OrganizationMember)*)
	'}')?;

OrganizationUnitType:
	'unit-type' name=ID ('{' 
		(('name' longName=STRING)? & 
		('description' description=STRING)?) 
	'}')?
;

OrganizationMember:
	OrganizationUnit | BusinessRole | Person | Worker | Group | Partnership | OrganizationUnitType;

OrganizationUnit:
	'unit' name=Fqn ('subunit of' parent=[OrganizationUnit|Fqn])?
	('{'
		(('name' longName=STRING)? &
		('description' description=STRING)? &
		('type' types+=[OrganizationUnitType])?)
	'}')?;
	
BusinessRole:
	'role' name=Fqn
	('{'
		(('name' longName=STRING)? &
		('description' description=STRING)?)
	'}')?;

Person:
	'person' name=ID
	('{'
		(('first name' firstName=STRING)? &
		('middle name' middleName=STRING)? &
		('last name' lastName=STRING)? &
		('email' email=STRING)?)
	'}')?
;

Worker:
// must filter persons. must show only Partners and Own Company persons.
	'worker' person=[Person|Fqn] ('{' 
		(('allocatedOn' allocationUnit=[OrganizationUnit|Fqn])? & 
    	('playRoles' '('playRoles+=[BusinessRole|Fqn] (',' playRoles+=[BusinessRole|Fqn])* ')')?) 
	'}')?
;

Group:
	'group' name=ID ('{'
		(('name' longName=STRING)? &
		('description' description=STRING)?)
		('worker' workers+=[Worker|Fqn])* 
	'}')?
;

Partnership:
	'partnership' 'with' company=[Organization|Fqn] ('{' 
		('responsible' responsible=[Worker|Fqn])? 
		('agreement'agreement=STRING)? 
	'}')?;

FqnWithWildCard:
	Fqn ('.' '*')?;

Fqn:
	ValidID (=> '.' ValidID)*;

ValidID:
	ID;
