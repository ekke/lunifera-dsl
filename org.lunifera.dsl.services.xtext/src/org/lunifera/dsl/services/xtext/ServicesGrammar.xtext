grammar org.lunifera.dsl.services.xtext.ServicesGrammar with org.lunifera.dsl.common.xtext.CommonGrammar

import "http://www.lunifera.org/dsl/dto/v1" as dto
import "http://www.lunifera.org/dsl/services/v1" as service
import "http://www.lunifera.org/dsl/common/types/v1" as common
import "http://www.lunifera.org/dsl/entity/v1" as entity
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.eclipse.org/Xtext/Xbase/XAnnotations" as xannotation
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

LServiceModel returns service::LServiceModel:
	packages+=TypedPackage*;

Class returns service::LDTOService:
	{service::LDTOService} annotations+=AnnotationDef*
	{service::LDTOService.annotationInfo=current}
	'dtoservice' name=ValidIDWithKeywords 'provides' dto=[dto::LDto]
	'{'
	('persistenceID' persistenceId=QualifiedName)?
	injectedServices=InjectedServices
//	((((filterable=FilterableAttributes)? & (sortable=SortableAttributes)?)) | ';')
	('get' getExpression=XBlockExpression)? 
	('find' findExpression=XBlockExpression)? 
	('findWithDelimiter' findExpressionWithDelimiter=XBlockExpression)? 
	('update' updateExpression=XBlockExpression)? 
	('delete' deleteExpression=XBlockExpression)?
	'}';
 
InjectedServices returns service::LInjectedServices:
	{service::LInjectedServices}
	(services+=InjectedService)*;

InjectedService returns service::LInjectedService:
	{service::LInjectedService}
	'injectService' (cardinality=Cardinality)? service=JvmTypeReference attributeName=ID ';';

FilterableAttributes returns service::LFilterableAttributes:
	'filterable' '{'
	filterableFeatures+=[dto::LDtoFeature|LFQN] (',' filterableFeatures+=[dto::LDtoFeature|LFQN])* ';'
	'}';

SortableAttributes returns service::LSortableAttributes:
	'sortable' '{'
	sortableFeatures+=[dto::LDtoFeature|LFQN] (',' sortableFeatures+=[dto::LDtoFeature|LFQN])* ';'
	'}';

enum Cardinality returns service::LCardinality:
	ZERO_TO_ONE="optional" | ONE_TO_ONE="mandatory" | ZERO_TO_MANY="zeroToMany" | ONE_TO_MANY="oneToMany";