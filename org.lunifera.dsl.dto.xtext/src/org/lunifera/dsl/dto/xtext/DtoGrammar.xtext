grammar org.lunifera.dsl.dto.xtext.DtoGrammar with org.lunifera.dsl.common.xtext.CommonGrammar

import "http://www.lunifera.org/dsl/dto/v1" as dto
import "http://www.lunifera.org/dsl/common/types/v1" as common
import "http://www.lunifera.org/dsl/entity/v1" as entity
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.eclipse.org/Xtext/Xbase/XAnnotations" as xannotation
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

LDTOModel returns dto::LDtoModel:
	packages+=TypedPackage*;

Class returns common::LClass:
	{common::LClass} annotations+=AnnotationDef*
	{dto::LDto.annotationInfo=current}
	'dto' name=ValidIDWithKeywords ('extends' superType=[dto::LDto])? ('wraps' wrappedType=[common::LType])?
	'{'
	features+=DtoFeature*
	'}';

DtoFeature returns dto::LDtoFeature:
	{dto::LDtoFeature}
	annotations+=AnnotationDef*
	(({dto::LDtoReference.annotationInfo=current}
	('ref' (cascading?='cascade')? type=[dto::LDto] multiplicity=Multiplicity? name=ValidIDWithKeywords
	('opposite' opposite=[dto::LDtoReference|LFQN])?)
	';')
	|
	({dto::LDtoAttribute.annotationInfo=current}
	(('transient' type=[common::LScalarType] name=ValidIDWithKeywords)
	|
	(('var' | id?='id' | version?="version") type=[common::LScalarType] multiplicity=Multiplicity?
	name=ValidIDWithKeywords
	';')))
	|
	({dto::LDtoOperation.annotationInfo=current}
	('def' type=JvmTypeReference name=ValidIDWithKeywords
	'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
	body=XExpression))
	|
	({dto::LDtoWrappedReference.annotationInfo=current}
	('uses' feature=[common::LFeature]
	('as' asType=[common::LScalarType]
	(('toDTO' convertToDTO=XBlockExpression) &
	('fromDTO' convertFromDTO=XBlockExpression)))?)
	';'));


